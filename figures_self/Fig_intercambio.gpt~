reset

set term epslatex size 5,5 color colortext standalone
set output "Fig_intercambio.tex"

TOP=0.99
BOTTOM=0.01
LEFT=0.01
RIGHT=0.99

set tmargin at screen TOP
set bmargin at screen BOTTOM
set lmargin at screen LEFT
set rmargin at screen RIGHT



set border 0
set notics
set multiplot
X0=0;
Y0=0;

s=0.3
set style arrow 1 back head filled size 0.1,20 lt 4 lw 1
middlearrow(from_x,from_y,to_x,to_y)=\
sprintf('set arrow from %f,%f to %f,%f as 1 nohead;',s*from_x+(1-s)*to_x,s*from_y+(1-s)*to_y,to_x,to_y).\
sprintf('set arrow from %f,%f to %f,%f as 1', from_x,from_y,s*from_x+(1-s)*to_x,s*from_y+(1-s)*to_y)

#eval(middlearrow(0.1,0.1,0.9,0.9))

junta(t1,t2)=middlearrow(X(t1),Y(t1),X(t2),Y(t2))

set size ratio 1
unset key

N=16

# Linea desde (0,0) a (3,2.5)

set param
set xrange [-1:4]
set yrange [-2.5:2.5]
set trange [0:N]
set samples N+1

a=2*pi/N

X(t)=cos(a*t)+X0
Y(t)=sin(a*t)+Y0

do for [i=0:N-1] {
eval(junta(i,i+1))
}

set object circle at (X(1)+X(2))/2,(Y(1)+Y(2))/2 size 0.15 fc rgb "#E0E0E0" fillstyle solid
set object circle at (X(6)+X(7))/2,(Y(6)+Y(7))/2 size 0.15 fc rgb "#E0E0E0" fillstyle solid
set object circle at (X(12)+X(13))/2,(Y(12)+Y(13))/2 size 0.15 fc rgb "#E0E0E0" fillstyle solid

plot X(t),Y(t) w p pt 7 ps 2 lc rgb "#0000FF"

X0=3
Y0=1.5

set label "YES" at 0.5*X0,0.5*Y0+0.15 center textcolor rgb "#00B000"
set arrow from 0.4*X0,0.4*Y0 to 0.6*X0,0.6*Y0 lw 4 lc rgb "#00B000" 

do for [i=2:5] {eval(junta(i,i+1))}
do for [i=7:11]{eval(junta(i,i+1))}
do for [i=13:16] {eval(junta(i,i+1))}
eval(junta(6,13))
eval(junta(1,7))
eval(junta(12,2))


plot X(t),Y(t) w p pt 7 ps 2 lc rgb "#0000FF"



X0=3
Y0=-1.5

set label "NO" at 0.5*X0,0.5*Y0+0.15 center textcolor rgb "#C00000"
set arrow from 0.4*X0,0.4*Y0 to 0.6*X0,0.6*Y0 lw 4 lc rgb "#C00000"

do for [i=2:5] {eval(junta(i,i+1))}
do for [i=7:11]{eval(junta(i,i+1))}
do for [i=13:16] {eval(junta(i,i+1))}

eval(junta(6,2))
eval(junta(12,7))
eval(junta(1,13))

plot X(t),Y(t) w p pt 7 ps 2 lc rgb "#0000FF"




